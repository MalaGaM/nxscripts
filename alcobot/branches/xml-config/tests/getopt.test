#
# AlcoBot - Alcoholicz site bot.
# Copyright (c) 2005 Alcoholicz Scripting Team
#
# Module Name:
#   Option Parsing Tests
#
# Author:
#   neoxed (neoxed@gmail.com) Sep 8, 2005
#
# Abstract:
#   Implements regression tests for option parsing functions.
#

package require tcltest 2
namespace import -force ::tcltest::*

proc ArrayGetSort {varName} {
    upvar $varName var
    set contents [list]
    foreach name [lsort [array names var]] {
        lappend contents $name $var($name)
    }
    return $contents
}

################################################################################
# GetElementFromList                                                           #
################################################################################

test getopt-1.1 {GetElementFromList: no args} {
    list [catch {GetElementFromList} msg] $msg
} {1 {wrong # args: should be "GetElementFromList list element ?type?"}}

test getopt-1.2 {GetElementFromList: match} {
    GetElementFromList {zero one two three four} two
} {two}

test getopt-1.3 {GetElementFromList: partial match} {
    GetElementFromList {zero one two three four} thr
} {three}

test getopt-1.4 {GetElementFromList: no match} {
    list [catch {GetElementFromList {zero one two three four} . blah} msg] $msg
} {1 {invalid blah ".", must be zero, one, two, three, or four}}

################################################################################
# GetIndexFromList                                                             #
################################################################################

test getopt-2.1 {GetIndexFromList: no args} {
    list [catch {GetIndexFromList} msg] $msg
} {1 {wrong # args: should be "GetIndexFromList list element"}}

test getopt-2.2 {GetIndexFromList: exact match} {
    GetIndexFromList {zero one two three four} two
} {2}

test getopt-2.3 {GetIndexFromList: no exact match} {
    GetIndexFromList {zero one two three four} .
} {-1}

test getopt-2.4 {GetIndexFromList: unique partial match} {
    GetIndexFromList {zero one two three four} thr
} {3}

test getopt-2.5 {GetIndexFromList: no unique partial match} {
    GetIndexFromList {zero one two three four} t
} {-1}

################################################################################
# GetOptions                                                                   #
################################################################################

test getopt-3.1 {GetOptions: no args} {
    list [catch {GetOptions} msg] $msg
} {1 {wrong # args: should be "GetOptions argList optList resultVar"}}

test getopt-3.2 {GetOptions: result variable as scalar} {
    unset -nocomplain result
    set result ""
    list [catch {GetOptions "-one 5 blah blah" {{one integer}} result} msg] $msg
} {1 {the variable "result" is not an array}}

test getopt-3.3 {GetOptions: invalid type} {
    -body {
        unset -nocomplain result
        list [catch {GetOptions "" {one {two .} three} result} msg] $msg
    }
    -match glob
    -result {1 {invalid option definition "two .":  bad type ".", must be *}}
}

test getopt-3.4 {GetOptions: invalid option} {
    unset -nocomplain result
    list [catch {GetOptions "-four blah blah" {one two three} result} msg] $msg
} {1 {invalid option "-four"}}

test getopt-3.5 {GetOptions: end of options} {
    unset -nocomplain result
    list [GetOptions "-one -- -three blah blah" {one two three} result] [ArrayGetSort result]
} {{-three blah blah} {one {}}}

test getopt-3.6 {GetOptions: options with no values} {
    unset -nocomplain result
    list [GetOptions "-one -three blah blah" {one two three} result] [ArrayGetSort result]
} {{blah blah} {one {} three {}}}

test getopt-3.7 {GetOptions: any value not present} {
    unset -nocomplain result
    list [catch {GetOptions "-two -one" {{one arg} two {three arg}} result} msg] $msg
} {1 {the option "-one" requires a value}}

test getopt-3.8 {GetOptions: any value} {
    unset -nocomplain result
    list [GetOptions "-one okay -three -- blah blah" {{one arg} two three} result] [ArrayGetSort result]
} {{blah blah} {one okay three {}}}

test getopt-3.9 {GetOptions: invalid typed value} {
    unset -nocomplain result
    list [catch {GetOptions "-one . -three" {{one integer} two three} result} msg] $msg
} {1 {the option "-one" requires a integer type value}}

test getopt-3.10 {GetOptions: typed value} {
    unset -nocomplain result
    list [GetOptions "-one 5 -three blah blah" {{one integer} two three} result] [ArrayGetSort result]
} {{blah blah} {one 5 three {}}}

test getopt-3.11 {GetOptions: invalid prefined value} {
    unset -nocomplain result
    list [catch {GetOptions "-one 5 -type . -three" {{one integer} {type arg {exact glob regexp}} three} result} msg] $msg
} {1 {invalid value ".", must be exact, glob, or regexp}}

test getopt-3.12 {GetOptions: prefined value, exact match} {
    unset -nocomplain result
    list [GetOptions "-one 5 -type regexp -three blah blah" {{one integer} {type arg {exact glob regexp}} three} result] [ArrayGetSort result]
} {{blah blah} {one 5 three {} type regexp}}

test getopt-3.13 {GetOptions: prefined value, partial match} {
    unset -nocomplain result
    list [GetOptions "-one 5 -type ex -three blah blah" {{one integer} {type arg {exact glob regexp}} three} result] [ArrayGetSort result]
} {{blah blah} {one 5 three {} type exact}}


unset -nocomplain result
::tcltest::cleanupTests
return
